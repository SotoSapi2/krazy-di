plugins {
    id 'java'
    id "com.vanniktech.maven.publish" version "0.36.0"
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly("org.projectlombok:lombok:1.18.42")
    annotationProcessor("org.projectlombok:lombok:1.18.42")

    testCompileOnly("org.projectlombok:lombok:1.18.42")
    testAnnotationProcessor("org.projectlombok:lombok:1.18.42")
    implementation 'org.jetbrains:annotations:24.0.1'

    testImplementation(platform("org.junit:junit-bom:5.13.2"))

    testImplementation("org.junit.jupiter:junit-jupiter") {
        because 'allows to write and run Jupiter tests'
    }

    testImplementation("junit:junit:4.13.2")
    testRuntimeOnly("org.junit.vintage:junit-vintage-engine") {
        because 'allows JUnit 3 and JUnit 4 tests to run'
    }

    testRuntimeOnly("org.junit.platform:junit-platform-launcher") {
        because 'allows tests to run from IDEs that bundle older version of launcher'
    }
}

tasks.withType(Javadoc).configureEach {
    options.addStringOption('Xdoclint:-missing', '-quiet')
}

test {
    useJUnitPlatform()
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible())
    {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion)
    {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }

    withSourcesJar()
}

mavenPublishing {
    coordinates(project.maven_group, project.archives_base_name, project.project_version)
    publishToMavenCentral()

    pom {
        name = project.archives_base_name
        description = "Simple Java dependency injection library inspired by .NET dependency injection system."
        url = "https://${project.project_git_url}"

        licenses {
            license {
                name = "MIT"
            }
        }

        developers {
            developer {
                id = project.project_owner_name
                name = project.project_owner_name
                url = "https://${project.project_git_url}"
            }
        }

        scm {
            url = "https://${project.project_git_url}"
            connection = "scm:git:git://${project.project_git_url}.git"
            developerConnection  = "scm:git:ssh://git@${project.project_git_url}.git"
        }
    }

    signAllPublications()
}